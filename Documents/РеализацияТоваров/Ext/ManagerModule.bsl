// Печать накладной с сортировкой строк
Процедура ПечатьНакладной(ТабДок, Ссылка, ВариантСортировки) Экспорт
	
	Макет = Документы.РеализацияТоваров.ПолучитьМакет("ПечатьНакладной");
	
	// Берем первый документ, если пришел массив
	Если ТипЗнч(Ссылка) = Тип("Массив") Тогда
		СсылкаНаДокумент = Ссылка[0];
	Иначе
		СсылкаНаДокумент = Ссылка;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	| РеализацияТоваров.Номер КАК Номер,
	| РеализацияТоваров.Дата КАК Дата,
	| РеализацияТоваров.Клиент КАК Клиент,
	| РеализацияТоваров.СуммаИтого КАК СуммаИтого,
	| РеализацияТоваровТовары.НомерСтроки КАК НомерСтроки,
	| РеализацияТоваровТовары.Товар.Наименование КАК Товар,
	| РеализацияТоваровТовары.Цена КАК Цена,
	| РеализацияТоваровТовары.Количество КАК Количество,
	| РеализацияТоваровТовары.Сумма КАК Сумма
	|ИЗ
	| Документ.РеализацияТоваров КАК РеализацияТоваров
	| ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
	| ПО РеализацияТоваров.Ссылка = РеализацияТоваровТовары.Ссылка
	|ГДЕ
	| РеализацияТоваров.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаДокумент);
	Результат = Запрос.Выполнить();
	
	ТаблицаТоваров = Результат.Выгрузить();
	ТаблицаТоваров.Колонки.Добавить("КлючСортировки");
	
	Для Каждого Строка Из ТаблицаТоваров Цикл
		
		Если ВариантСортировки = "ПоДлинеНаименования" Тогда
			Строка.КлючСортировки = СтрДлина(Строка.Товар);
		ИначеЕсли ВариантСортировки = "ПоНаименованию" Тогда
			Строка.КлючСортировки = Строка.Товар;
		ИначеЕсли ВариантСортировки = "ПоЦене" Тогда
			Строка.КлючСортировки = Строка.Цена;
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаТоваров.Сортировать("КлючСортировки Возр");
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	Шапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьТоварыШапка = Макет.ПолучитьОбласть("ТоварыШапка");
	ОбластьТовары = Макет.ПолучитьОбласть("Товары");
	Подвал = Макет.ПолучитьОбласть("Подвал");
	
	ТабДок.Очистить();
	
	ПерваяСтрока = ТаблицаТоваров[0];
	ОбластьЗаголовок.Параметры.Номер = Число(ПерваяСтрока.Номер);
	ОбластьЗаголовок.Параметры.Дата = Формат(ПерваяСтрока.Дата, "ДЛФ=D");
	ТабДок.Вывести(ОбластьЗаголовок);
	
	Шапка.Параметры.Клиент = ПерваяСтрока.Клиент;
	ТабДок.Вывести(Шапка);
	
	ТабДок.Вывести(ОбластьТоварыШапка);
	
	Для Каждого СтрокаТовара Из ТаблицаТоваров Цикл
		ОбластьТовары.Параметры.Заполнить(СтрокаТовара);
		ТабДок.Вывести(ОбластьТовары);
	КонецЦикла;
	
	Подвал.Параметры.СуммаИтого = ПерваяСтрока.СуммаИтого;
	ТабДок.Вывести(Подвал);
	
КонецПроцедуры